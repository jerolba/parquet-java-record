plugins {
    id 'java'
    id 'jacoco'
	id 'checkstyle'
    id 'maven-publish'
    id 'signing'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
}

dependencies {
	implementation 'org.apache.avro:avro:1.10.1'
	
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

test {
    useJUnitPlatform()
}

task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            customizePom(pom)
            groupId 'com.jerolba'
            artifactId 'avro-record'
            version '0.0.1'

            from components.java

            artifact sourceJar
            artifact javadocJar
        }
    }
    if (project.hasProperty('sonatypeUsername')){
	    repositories {
	        maven {
	            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
	            //url 'https://oss.sonatype.org/content/repositories/snapshots/'
	            credentials {
	                username sonatypeUsername
	                password sonatypePassword
	            }
	        }
	    }    
    } else {
		println 'No sonatype configuration found. Expected user and password at $HOME/.gradle/gradle.properties file.'    	
    }    
}

signing {
    sign publishing.publications.mavenJava
}

def customizePom(pom) {
    pom.withXml {
        def root = asNode()

        // add all items necessary for maven central publication
        root.children().last() + {
            resolveStrategy = Closure.DELEGATE_FIRST

            description 'Avro Record serialize/deserialize Avro files using Java Records'
            name 'Avro Record'
            url 'https://github.com/jerolba/parquet-java-record'
            licenses {
                license {
                    name 'Apache License 2.0'
                    url 'http://www.apache.org/license/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            scm {
                url 'https://github.com/jerolba/parquet-java-record'
                connection 'scm:git:git://github.com/jerolba/parquet-java-record'
                developerConnection 'scm:git:ssh://github.com:jerolba/parquet-java-record'
            }
            developers {
                developer {
                    id 'jerolba'
                    name 'Jerónimo López'
                }
            }
        }
    }
}
